pkgname=unspoken-git
pkgver=0.1.0
pkgrel=1
pkgdesc="A lightweight ActivityPub microblogging server"
arch=('x86_64')
url="https://github.com/MetroWind/unspoken"
license=('MIT')
groups=()
depends=('sqlite' 'curl' 'openssl')
makedepends=('git' 'cmake' 'gcc')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=()
backup=("etc/unspoken.yaml")
options=(!debug !strip)
source=("git+https://github.com/MetroWind/unspoken.git"
        "unspoken.sysusers"
        "unspoken.service"
        "unspoken.tmpfiles")

sha256sums=('SKIP'
            'fa5e506b9f38111e022400f22f7f07ecf4c22d14170ef2f299e2d36ab02b8a3c'
            '2a9d055c1bde413534474299a7617adf311578060765a017def1da2f43909ea2'
            'd77b6de2abdefa0a5b8e4baae4fdcc699e1a51bcfb68c1dfddb0609834f5ecd5')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
    cd "$srcdir/${pkgname%-git}"
    cmake -B build \
          -DCMAKE_BUILD_TYPE='Release' \
          -DCMAKE_INSTALL_PREFIX='/usr' \
          -Wno-dev .
    cmake --build build
}

package() {
    cd "$srcdir/${pkgname%-git}"

    # Install binary
    install -Dm755 build/unspoken "$pkgdir/usr/bin/unspoken"

    # Install static assets and templates
    install -d "$pkgdir/var/lib/unspoken"
    cp -r static templates "$pkgdir/var/lib/unspoken/"

    # Install config
    install -Dm640 config.yaml.example "$pkgdir/etc/unspoken.yaml"

    # Install systemd support files
    install -Dm644 "$srcdir/unspoken.sysusers" "$pkgdir/usr/lib/sysusers.d/unspoken.conf"
    install -Dm644 "$srcdir/unspoken.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/unspoken.conf"
    install -Dm644 "$srcdir/unspoken.service" "$pkgdir/usr/lib/systemd/system/unspoken.service"
}
